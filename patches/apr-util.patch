diff --git "a/CMakeLists.txt" "b/CMakeLists.txt"
index ed04f4c..95b35ec 100644
--- "a/CMakeLists.txt"
+++ "b/CMakeLists.txt"
@@ -20,6 +20,7 @@
 CMAKE_MINIMUM_REQUIRED(VERSION 3.5)
 
 PROJECT(APR-Util C)
+set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake ${CMAKE_MODULE_PATH})
 
 INCLUDE(CMakeDependentOption)
 
@@ -41,6 +42,7 @@ option(APU_HAVE_ICONV       "Xlate support"                             OFF)
 OPTION(APU_HAVE_ODBC        "Build ODBC DBD driver"                     ON)
 OPTION(APU_HAVE_SQLITE3     "Build SQLite3 DBD driver"                  OFF)
 OPTION(APU_HAVE_PGSQL       "Build PostgreSQL DBD driver"               OFF)
+OPTION(APU_HAVE_MYSQL       "Build MySQL DBD driver"                    ON)
 OPTION(APR_HAS_LDAP         "LDAP support"                              OFF)
 OPTION(INSTALL_PDB          "Install .pdb files (if generated)"         ON)
 OPTION(APU_BUILD_TEST       "Build the test suite"                      OFF)
@@ -86,6 +88,10 @@ IF(APU_HAVE_PGSQL)
   FIND_PACKAGE(PostgreSQL REQUIRED)
 ENDIF()
 
+IF(APU_HAVE_MYSQL)
+  FIND_PACKAGE(MySQL)
+ENDIF()
+
 # create 1-or-0 representation of feature tests for apu.h
 
 SET(apu_have_apr_iconv_10 0) # not yet implemented
@@ -152,6 +158,7 @@ SET(APR_PUBLIC_HEADERS_STATIC
   include/apr_ldap_option.h
   include/apr_ldap_rebind.h
   include/apr_ldap_url.h
+  include/apr_ldap_common.h
   include/apr_md4.h
   include/apr_md5.h
   include/apr_memcache.h
@@ -350,6 +357,14 @@ IF(APU_HAVE_PGSQL)
   ADD_COMPILE_DEFINITIONS(HAVE_LIBPQ_FE_H)
 ENDIF()
 
+IF(APU_HAVE_MYSQL)
+  LIST(APPEND dbd_drivers mysql)
+
+  ADD_APU_MODULE(apr_dbd_mysql-1 "apr_dbd_mysql"
+                 "dbd/apr_dbd_mysql.c"
+                 ${MYSQL_LIBRARY})
+ENDIF()
+
 ADD_LIBRARY(libaprutil-1 ${APR_SOURCES} ${APU_EXTRA_SOURCES} ${APR_PUBLIC_HEADERS_GENERATED})
 LIST(APPEND install_targets libaprutil-1)
 TARGET_LINK_LIBRARIES(libaprutil-1
@@ -471,6 +486,7 @@ MESSAGE(STATUS "  C compiler ...................... : ${CMAKE_C_COMPILER}")
 MESSAGE(STATUS "  DBD ODBC driver ................. : ${APU_HAVE_ODBC}")
 MESSAGE(STATUS "  DBD SQLite3 driver .............. : ${APU_HAVE_SQLITE3}")
 MESSAGE(STATUS "  DBD PostgreSQL .................. : ${APU_HAVE_PGSQL}")
+MESSAGE(STATUS "  DBD MySQL ....................... : ${APU_HAVE_MYSQL}")
 MESSAGE(STATUS "  APU_HAVE_CRYPTO ................. : ${APU_HAVE_CRYPTO}")
 MESSAGE(STATUS "  APU_HAVE_ICONV .................. : ${APU_HAVE_ICONV}")
 MESSAGE(STATUS "  APR_HAS_LDAP .................... : ${APR_HAS_LDAP}")

diff --git a/cmake/FindMySQL.cmake b/cmake/FindMySQL.cmake
new file mode 100644
index 00000000..99d1c61e
--- /dev/null
+++ b/cmake/FindMySQL.cmake
@@ -0,0 +1,83 @@
+# Distributed under the OSI-approved BSD 3-Clause License.  See accompanying
+# file COPYING-CMAKE-SCRIPTS or https://cmake.org/licensing for details.
+
+#.rst
+# Get from http://www.cmake.org/Wiki/CMakeUserFindMySQL
+# - Find mysqlclient
+# Find the native MySQL includes and library
+#
+#  MYSQL_INCLUDE_DIRS- where to find mysql.h, etc.
+#  MYSQL_LIBRARIES   - List of libraries when using MySQL.
+#  MYSQL_FOUND       - True if MySQL found.
+
+find_path(MYSQL_INCLUDE_DIR
+          NAMES mysql.h
+          PATH_SUFFIXES mysql
+          DOC "MySQL Client library includes")
+
+if( MYSQL_INCLUDE_DIR AND EXISTS "${MYSQL_INCLUDE_DIR}/mysql_version.h" )
+    file( STRINGS "${MYSQL_INCLUDE_DIR}/mysql_version.h"
+          MYSQL_VERSION_H REGEX "^#define[ \t]+MYSQL_SERVER_VERSION[ \t]+\"[^\"]+\".*$" )
+    string( REGEX REPLACE
+            "^.*MYSQL_SERVER_VERSION[ \t]+\"([^\"]+)\".*$" "\\1" MYSQL_VERSION_STRING
+            "${MYSQL_VERSION_H}" )
+endif()
+
+find_library(MYSQL_LIBRARY NAMES mysqlclient mysqlclient_r)
+
+if( NOT CMAKE_C_COMPILER_LOADED )
+    message(AUTHOR_WARNING "C language not enabled: Skipping detection of extra link libraries.")
+elseif( MYSQL_INCLUDE_DIR AND MYSQL_LIBRARY )
+    # On Conda, mysqlclient is a static lib that requires explicit linking to zlib and zstd
+    function(check_mysql_test_program_links)
+        include(CheckCSourceCompiles)
+        include(CMakePushCheckState)
+        cmake_push_check_state(RESET)
+        set(CMAKE_REQUIRED_QUIET "yes")
+        set(CMAKE_REQUIRED_INCLUDES ${MYSQL_INCLUDE_DIR})
+        set(CMAKE_REQUIRED_LIBRARIES ${MYSQL_LIBRARY})
+        check_c_source_compiles("#include <mysql.h>\n; int main () {mysql_library_end (); return 0;}" MYSQL_TEST_PROGRAM_LINKS)
+        cmake_pop_check_state()
+    endfunction()
+
+    check_mysql_test_program_links()
+    if(NOT MYSQL_TEST_PROGRAM_LINKS)
+       find_library(_MYSQL_ZLIB_LIBRARY NAMES zlib)
+       find_library(_MYSQL_ZSTD_LIBRARY NAMES zstd)
+       if(_MYSQL_ZLIB_LIBRARY AND _MYSQL_ZSTD_LIBRARY)
+            function(check_mysql_test_program_links_with_zlib_and_zstd)
+                include(CheckCSourceCompiles)
+                include(CMakePushCheckState)
+                cmake_push_check_state(RESET)
+                set(CMAKE_REQUIRED_QUIET "yes")
+                set(CMAKE_REQUIRED_INCLUDES ${MYSQL_INCLUDE_DIR})
+                set(CMAKE_REQUIRED_LIBRARIES ${MYSQL_LIBRARY} ${_MYSQL_ZSTD_LIBRARY} ${_MYSQL_ZLIB_LIBRARY})
+                check_c_source_compiles("#include <mysql.h>\n;int main () {mysql_library_end (); return 0;}" MYSQL_TEST_PROGRAM_LINKS_WITH_ZLIB_AND_ZSTD)
+                cmake_pop_check_state()
+            endfunction()
+
+            check_mysql_test_program_links_with_zlib_and_zstd()
+            if(MYSQL_TEST_PROGRAM_LINKS_WITH_ZLIB_AND_ZSTD)
+              set(MYSQL_ZSTD_LIBRARY ${_MYSQL_ZSTD_LIBRARY})
+              set(MYSQL_ZLIB_LIBRARY ${_MYSQL_ZLIB_LIBRARY})
+            endif()
+       endif()
+    endif()
+endif()
+
+include(FindPackageHandleStandardArgs)
+find_package_handle_standard_args(MySQL
+                                  FOUND_VAR MYSQL_FOUND
+                                  REQUIRED_VARS MYSQL_LIBRARY MYSQL_INCLUDE_DIR
+                                  VERSION_VAR MYSQL_VERSION_STRING)
+include(FeatureSummary)
+set_package_properties(MYSQL PROPERTIES
+                       DESCRIPTION "MySQL Client library"
+                       URL "https://dev.mysql.com/downloads/c-api/"
+                       )
+
+mark_as_advanced(MYSQL_LIBRARY MYSQL_INCLUDE_DIR)
+
+set(MYSQL_LIBRARIES ${MYSQL_LIBRARY} ${MYSQL_ZSTD_LIBRARY} ${MYSQL_ZLIB_LIBRARY})
+set(MYSQL_INCLUDE_DIRS ${MYSQL_INCLUDE_DIR})
+
